use crate::ast::{Expr, SingleOp, MultipleOp};
use regex::Error as RegexError;
use regex::Regex;
use lalrpop_util::ParseError;

grammar;

extern {
    type Error = RegexError;
}

pub Expr: Box<Expr> = {
    Expr ExprOp Factor => Box::new(Expr::MultipleOp(<>)),
    Factor,
};

ExprOp: MultipleOp = {
    "or" => MultipleOp::Or,
    "||" => MultipleOp::Or,
};

Factor: Box<Expr> = {
    Factor FactorOp TermOrNotTerm => Box::new(Expr::MultipleOp(<>)),
    TermOrNotTerm,
};

FactorOp: MultipleOp = {
    "and" => MultipleOp::And,
    "&&" => MultipleOp::And,
};

Term: Box<Expr> = {
    Name => Box::new(Expr::Name(<>)),
    Group => Box::new(Expr::Group(<>)),
    Type => Box::new(Expr::Type(<>)),
    "(" <Expr> ")"
};

TermOrNotTerm: Box<Expr> = {
    NotOp <Term> => Box::new(Expr::Not(<>)),
    Term
};

NotOp: () = {
    "not" => (),
    "!" => ()
};

Name: SingleOp = {
    "name" <SingleOpCode>,
}

Group: SingleOp = {
    "group" <SingleOpCode>,
}

Type: SingleOp = {
    "type" <SingleOpCode>,
}

SingleOpCode: SingleOp = {
    "is" <Str>  => SingleOp::Is(<>),
    "in" "[" <Strs> "]" => SingleOp::In(<>),
    "matches" <Reg> => SingleOp::Matches(<>),
}

Reg: Regex = {
    <s:r"/[^/]+/"> =>? {
        Regex::new(&s[1..s.len()-1])
            .map_err(|error| ParseError::User  {
                error
            })
    }
}

Str: String = {
    <s:r"[A-Za-z0-9_-]+"> => s.to_string()
}

Strs: Vec<String> = {
    <Str> => vec![<>],
    <strings:Strs> "," <string:Str> => {
        let mut strings = strings;
        strings.push(string);
        strings
    }
}
